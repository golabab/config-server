plugins {
    id 'org.springframework.boot' version '2.6.5'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'java'
    id 'com.google.cloud.tools.jib' version '3.2.0'
}

group = 'com.golabab'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '17'

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

repositories {
    mavenCentral()
}

ext {
    set('springCloudVersion', "2021.0.1")
}

dependencies {
    annotationProcessor 'org.springframework.boot:spring-boot-configuration-processor'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'

    implementation 'org.springframework.cloud:spring-cloud-config-server'
    implementation 'org.springframework.cloud:spring-cloud-starter-stream-rabbit'
    implementation 'org.springframework.cloud:spring-cloud-config-monitor'
    implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'

    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
}

dependencyManagement {
    imports {
        mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

String imageName = "${project.name}:${project.version}"
jib {
    allowInsecureRegistries = true
    to {
        image = imageName
    }
    container {
        ports = ['10020']
        labels = [maintainer: 'raul']
        creationTime='USE_CURRENT_TIMESTAMP'
    }
}

task runDockerLocal () {
    dependsOn('jibDockerBuild')
    doLast {
        def containerName = "$project.name" as String
        def networkName = 'golabab-infra'
        def networkIp = '172.19.0.102'
        def envFilePath = "$project.projectDir\\.env" as String
        def port = 10020

        exec{ commandLine "docker stop $containerName".split(' ') }
        exec{ commandLine "docker rm $containerName".split(' ') }
        exec{ commandLine "docker run -d -p $port:$port --env-file $envFilePath --network $networkName --ip $networkIp --name $containerName $imageName".split(' ') }

        System.out.println('\n Docker Build SUCCESS!')
    }
}